/* 
This do file generates manager FE based on worker's wage 5 years after the event. The manager FE is then used to construct another measure of H- and L-type managers.

Input:
    "${TempData}/04MainOutcomesInEventStudies_EarlyAgeM.dta" <== constructed in 0102 do file.

Output:
    "${TempData}/temp_Mngr_MngrFE.dta"
    "${TempData}/temp_workersubsample_MngrFE.dta"

RA: WWZ 
Time: 2024-10-30
*/

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 1. split event workers into two samples 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

use "${TempData}/04MainOutcomesInEventStudies_EarlyAgeM.dta", clear 

keep if FT_Rel_Time!=. 
keep if FT_Mngr_both_WL2==1
    //&? keep a worker panel that contains only the event workers 

sort IDlse YearMonth 
bysort IDlse: generate occurrence = _n 

set seed 1234 
generate subsample = runiform() if occurrence==1
replace  subsample = 0 if inrange(subsample, 0, 0.5)
replace  subsample = 1 if inrange(subsample, 0.5, 1)
    //&? a worker level (instead of worker-month level) randomization

sort IDlse YearMonth 
bysort IDlse: egen sample = mean(subsample)
drop subsample 
rename sample subsample 
order subsample, before(IDlse)

preserve 

    keep IDlse subsample
    duplicates drop 

    save "${TempData}/temp_workersubsample_MngrFE.dta", replace 

restore 

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 2. manager FE based on workers' wage 5 years after the event
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-2-1. calendar time of the event 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

generate FT_Event_Time = . 
replace  FT_Event_Time = FT_Calend_Time_LtoL if FT_Calend_Time_LtoL!=. & FT_Event_Time==.
replace  FT_Event_Time = FT_Calend_Time_LtoH if FT_Calend_Time_LtoH!=. & FT_Event_Time==.
replace  FT_Event_Time = FT_Calend_Time_HtoL if FT_Calend_Time_HtoL!=. & FT_Event_Time==.
replace  FT_Event_Time = FT_Calend_Time_HtoH if FT_Calend_Time_HtoH!=. & FT_Event_Time==.
format   FT_Event_Time %tm

generate FT_Event_Time_5yrsLater = FT_Event_Time + 60 
format   FT_Event_Time_5yrsLater %tm 

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-2-2. wage at 5 years after the event date  
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

sort IDlse YearMonth 
bysort IDlse: egen LogPayBonus_5yrsLater = mean(cond(YearMonth==FT_Event_Time_5yrsLater, LogPayBonus, .))

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 3. run regressions on a 
*??         cross-section and subsample of event workers (subsample==0)
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

keep if FT_Rel_Time==0

keep  subsample IDlse YearMonth FT_Event_Time IDlseMHR LogPayBonus_5yrsLater Office Func AgeBand Female
order subsample IDlse YearMonth FT_Event_Time IDlseMHR LogPayBonus_5yrsLater Office Func AgeBand Female 

reghdfe LogPayBonus_5yrsLater i.FT_Event_Time if subsample==0, absorb(Office##Func AgeBand##Female MngrFE=IDlseMHR)
    //&? 2535 obs in the regression 

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 4. store manager FE 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

keep IDlseMHR MngrFE 
drop if MngrFE==.
duplicates drop     
    //&? 871 observations 

summarize MngrFE, detail 
global median_MngrFE = r(p50)
global p75_MngrFE    = r(p75)

generate MngrFE_Med = (MngrFE >= ${median_MngrFE})
generate MngrFE_p75 = (MngrFE >= ${p75_MngrFE})

save "${TempData}/temp_Mngr_MngrFE.dta", replace 

