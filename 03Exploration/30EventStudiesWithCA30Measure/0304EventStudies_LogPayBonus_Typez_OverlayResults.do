/* 

This do file plots the event study coefficients for two outcomes of interest.

Input: 
    "${EventStudyResults}/Outcome3_TransferSJLC_Type1_Pre24Post84.dta"
    "${EventStudyResults}/Outcome4_LogPayBonus_Type1_Pre24Post84.dta"


RA: WWZ 
Time: 2025-05-21
*/

global EventStudyResults "${Results}/010EventStudiesResultsWithCA30Measure"

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 1. keep only necessary variables in the resulting dataset 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

use "${EventStudyResults}/Outcome4_LogPayBonus_Type1_Pre24Post84.dta", replace 
    keep quarter_LogPayBonus_gains coeff_LogPayBonus_gains lb_LogPayBonus_gains ub_LogPayBonus_gains
    generate quarter = quarter_LogPayBonus_gains
    drop if quarter==.
    save "${EventStudyResults}/Outcome4_LogPayBonus_Type1_Pre24Post84_ForMerge.dta", replace

use "${EventStudyResults}/Outcome3_TransferSJLC_Type1_Pre24Post84.dta", replace 
    keep quarter_TransferSJLC_gains coeff_TransferSJLC_gains lb_TransferSJLC_gains ub_TransferSJLC_gains
    generate quarter = quarter_TransferSJLC_gains
    drop if quarter==.
    save "${EventStudyResults}/Outcome3_TransferSJLC_Type1_Pre24Post84_ForMerge.dta", replace

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 2. merge the datasets  
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

use "${EventStudyResults}/Outcome4_LogPayBonus_Type1_Pre24Post84_ForMerge.dta", clear 
merge 1:1 quarter using "${EventStudyResults}/Outcome3_TransferSJLC_Type1_Pre24Post84_ForMerge.dta", nogenerate

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 3. plot the coefficients in one figure  
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

replace quarter_LogPayBonus_gains  = quarter_LogPayBonus_gains  - 0.2
replace quarter_TransferSJLC_gains = quarter_TransferSJLC_gains + 0.2

twoway ///
    (scatter coeff_LogPayBonus_gains quarter_LogPayBonus_gains, lcolor(ebblue) mcolor(ebblue)) ///
    (rcap lb_LogPayBonus_gains ub_LogPayBonus_gains quarter_LogPayBonus_gains, lcolor(ebblue)) ///
    (scatter coeff_TransferSJLC_gains quarter_TransferSJLC_gains, lcolor(magenta) mcolor(magenta)) ///
    (rcap lb_TransferSJLC_gains ub_TransferSJLC_gains quarter_TransferSJLC_gains, lcolor(magenta)) ///
    , yline(0, lcolor(maroon)) xline(-1, lcolor(maroon)) ///
    xlabel(-8(2)28, grid gstyle(dot) labsize(medsmall)) /// 
    ylabel(-0.3(0.05)0.3, grid gstyle(dot) labsize(medsmall)) ///
    xtitle("Quarters since manager change", size(medlarge)) ytitle("Coefficient values", size(medlarge)) ///
    legend(label(2 "Pay + bonus (in logs)") label(4 "Lateral moves") order(2 4) position(6) ring(0) size(small))
graph export "${EventStudyResults}/Outcome3And4_LogPayBonusAndTransferSJLC_Coef1_Gains.pdf", replace