/* 
This do file plots the number and fraction of new hires across years and WLs.

RA: WWZ 
Time: 2025-03-28
*/

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 1. create a relevant dataset 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

use "${TempData}/04MainOutcomesInEventStudies.dta", clear 
keep IDlse YearMonth Year WL Tenure

keep if inrange(WL, 1, 6)
replace WL=5 if WL>5 & WL!=.

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-1-1. identify new hires in a given year 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

sort IDlse YearMonth
bysort IDlse Year: egen TenureMin = min(Tenure)
generate NewHire = (TenureMin < 2)

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-1-2. reduce it to a individual-year level dataset 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

sort IDlse YearMonth
egen tag_ID_Year = tag(IDlse Year)
keep if tag_ID_Year == 1
    //&? for each employee in a given year, keep only one observation 

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-1-3. collapse it to a year-WL level dataset 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

generate one = 1
collapse (sum) TotalNumber=one TotalNewHire=NewHire (mean) FracNewHire=NewHire, by(Year WL)

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 2. draw the plot 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

order WL Year
sort  WL Year

twoway ///
    (connected TotalNewHire Year if WL==1) ///
    (connected TotalNewHire Year if WL==2) ///
    (connected TotalNewHire Year if WL==3) ///
    (connected TotalNewHire Year if WL==4) ///
    (connected TotalNewHire Year if WL==5) ///
    , legend(label(1 "WL1") label(2 "WL2") label(3 "WL3") label(4 "WL4") label(5 "WL5+") cols(1) position(3)) ///
    xscale(range(2011(1)2021)) xlabel(2011(1)2021,labsize(medsmall)) xtitle("Year", size(medlarge)) ///
    scheme(tab2) ///
    ytitle("Total number of new hires", size(medlarge)) ylabel(0(5000)30000, grid gstyle(dot)) 
graph export "${Results}/NewHiresAcrossYearsAndWLs_Counts.png", replace as(png)

twoway ///
    (connected FracNewHire Year if WL==1) ///
    (connected FracNewHire Year if WL==2) ///
    (connected FracNewHire Year if WL==3) ///
    (connected FracNewHire Year if WL==4) ///
    (connected FracNewHire Year if WL==5) ///
    , legend(label(1 "WL1") label(2 "WL2") label(3 "WL3") label(4 "WL4") label(5 "WL5+") cols(1) position(3)) ///
    xscale(range(2011(1)2021)) xlabel(2011(1)2021,labsize(medsmall)) xtitle("Year", size(medlarge)) ///
    scheme(tab2) ///
    ytitle("Fraction as new hires", size(medlarge)) ylabel(0(0.1)0.5, grid gstyle(dot)) 
graph export "${Results}/NewHiresAcrossYearsAndWLs_Fraction.png", replace as(png)

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 3. stacked bar plot 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

order Year WL TotalNewHire
sort  Year WL

label define WLAggroup 1 "WL1" 2 "WL2" 3 "WL3" 4 "WL4" 5 "WL5+"
label values WL WLAggroup
label variable Year "Year"

graph hbar TotalNewHire, over(WL) over(Year) stack asyvars percentage ///
    scheme(tab2) ///
    ylabel(0(10)100, labsize(small)) ytitle(Percentage among new hires, size(medlarge)) ///
    title("Distribution of new hires across work levels", size(medlarge))
graph export "${Results}/NewHiresAcrossYearsAndWLs_Dist_Fraction.png", replace as(png)

graph hbar TotalNewHire, over(WL) over(Year) stack asyvars ///
    scheme(tab2) ///
    ylabel(0(5000)35000, labsize(small)) ytitle(Number of new hires, size(medlarge)) ///
    title("Distribution of new hires across work levels", size(medlarge))
graph export "${Results}/NewHiresAcrossYearsAndWLs_Dist_Count.png", replace as(png)
