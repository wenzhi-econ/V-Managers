/* 
This do file generates relevant event dummies used for event studies.

Input:
    "${TempData}/FinalFullSample.dta"                   <== created in 0101_01 do file 
    "${TempData}/0103_01CrossSectionalEventWorkers.dta" <== created in 0103_01 do file 

Output:
    "${TempData}/0103_02EventWorkersPanel_WithEventDummies" <== main output 

Description of the output dataset:
    (1) The dataset contains only event workers.
    (2) It contains workers' event group based on the CA30 high-flyer measure.

RA: WWZ 
Time: 2025-04-14
*/

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 1. merge event dates to the outcome dataset 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

use "${TempData}/FinalFullSample.dta", clear 

merge m:1 IDlse using "${TempData}/0103_01CrossSectionalEventWorkers.dta"
keep if _merge==3
drop _merge
    //impt: I only keep employees that are in the event studies.
    //impt: I will use event study sample, event workers, and analysis sample interchangeably.

codebook IDlse
    //&? expected to be 29,826; and it is

order IDlse YearMonth IDlseMHR Event_Time Event_Time_1monthbefore IDMngr_Pre IDMngr_Post

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 2. merge pre- and post-event managers' quality measures 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

rename YearMonth YearMonth_Save

rename Event_Time YearMonth
merge m:1 IDMngr_Post YearMonth using "${TempData}/0102_03HFMeasure.dta"
    drop if _merge==2
    drop _merge 
rename CA30 CA30_Post
rename YearMonth Event_Time

rename Event_Time_1monthbefore YearMonth
merge m:1 IDMngr_Pre YearMonth using "${TempData}/0102_03HFMeasure.dta"
    drop if _merge==2
    drop _merge 
rename CA30 CA30_Pre
rename YearMonth Event_Time_1monthbefore

rename YearMonth_Save YearMonth

sort  IDlse YearMonth
order IDlse YearMonth IDlseMHR Event_Time Event_Time_1monthbefore IDMngr_Pre CA30_Pre IDMngr_Post CA30_Post

count if CA30_Pre==.
    //&?  1,520 observations have no information on pre-event manager type.
count if CA30_Post==.

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 3. event-relevant variables 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-3-1. Rel_Time
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

generate Rel_Time = YearMonth - Event_Time, after(Event_Time)
drop Event_Time_1monthbefore

label variable Rel_Time "Relative time to the event"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-3-2. classify each employee into four event groups 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

generate CA30_LtoL = .
replace  CA30_LtoL = 1 if CA30_Pre==0 & CA30_Post==0
replace  CA30_LtoL = 0 if CA30_Pre==0 & CA30_Post==1
replace  CA30_LtoL = 0 if CA30_Pre==1 & CA30_Post==1
replace  CA30_LtoL = 0 if CA30_Pre==1 & CA30_Post==0

generate CA30_LtoH = .
replace  CA30_LtoH = 0 if CA30_Pre==0 & CA30_Post==0
replace  CA30_LtoH = 1 if CA30_Pre==0 & CA30_Post==1
replace  CA30_LtoH = 0 if CA30_Pre==1 & CA30_Post==1
replace  CA30_LtoH = 0 if CA30_Pre==1 & CA30_Post==0

generate CA30_HtoH = .
replace  CA30_HtoH = 0 if CA30_Pre==0 & CA30_Post==0
replace  CA30_HtoH = 0 if CA30_Pre==0 & CA30_Post==1
replace  CA30_HtoH = 1 if CA30_Pre==1 & CA30_Post==1
replace  CA30_HtoH = 0 if CA30_Pre==1 & CA30_Post==0

generate CA30_HtoL = .
replace  CA30_HtoL = 0 if CA30_Pre==0 & CA30_Post==0
replace  CA30_HtoL = 0 if CA30_Pre==0 & CA30_Post==1
replace  CA30_HtoL = 0 if CA30_Pre==1 & CA30_Post==1
replace  CA30_HtoL = 1 if CA30_Pre==1 & CA30_Post==0

count if CA30_LtoL==.
count if CA30_LtoH==.
count if CA30_HtoH==.
count if CA30_HtoL==.
    //&? 1,520

codebook IDlse if CA30_LtoL!=.
    //&? 29,797 distinct event workers can be classified into one of the four event groups.

order IDlse YearMonth IDlseMHR Event_Time Rel_Time IDMngr_Pre IDMngr_Post CA30_Pre CA30_Post CA30_LtoL CA30_LtoH CA30_HtoH CA30_HtoL

label variable CA30_LtoL "LtoL event group (based on CA30 measure)"
label variable CA30_LtoH "LtoH event group (based on CA30 measure)"
label variable CA30_HtoH "HtoH event group (based on CA30 measure)"
label variable CA30_HtoL "HtoL event group (based on CA30 measure)"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-3-3. check the variables 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

drop if CA30_LtoL==. | CA30_LtoH==. | CA30_HtoH==. |  CA30_HtoL==.
    //&? drop 1,520 observations whose pre-event manager is not in the dataset in that event month 

codebook IDlse
    //&? a panel of event workers with identifiable event groups.
    //&? 29,797 distinct employees, with 1,911,659 employee-year-month observations

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 4. obtain a final version dataset for all event study results 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

order IDlse YearMonth IDlseMHR Rel_Time Event_Time ///
    CA30_LtoL CA30_LtoH CA30_HtoH CA30_HtoL ///
    IDMngr_Pre IDMngr_Post CA30_Pre CA30_Post ///
    TransferSJVC ChangeSalaryGradeC PromWLC LogPayBonus LogPay LogBonus

save "${TempData}/0103_02EventWorkersPanel_WithEventDummies.dta", replace