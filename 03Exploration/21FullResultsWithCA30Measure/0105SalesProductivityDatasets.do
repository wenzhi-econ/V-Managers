/* 
This do file creates a dataset with sales productivity variables.

Input:
    "${RawMNEData}/AllSnapshotWC.dta"         <== raw data
    "${RawMNEData}/CDProductivityMonth.dta"   <== raw data 
    "${RawMNEData}/CDProductivityQuarter.dta" <== raw data 
    "${RawMNEData}/CDProductivityYear.dta"    <== raw data 

Output:
    "${TempData}/0105SalesProdOutcomes.dta"

Description of the output dataset:
    (1) A panel of worker sample with the following variables: IDlse YearMonth Productivity ProductivityStd ChannelFE.
    (2) In particular, the normalized measure "ProductivityStd" will be used later, as an objective productivity measure.

RA: WWZ 
Time: 2024-11-19
*/

use "${RawMNEData}/AllSnapshotWC.dta", clear

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 1. monthly variables 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

merge 1:1 IDlse YearMonth using "${RawMNEData}/CDProductivityMonth" , keepusing(Productivity* ProdGroup File FreqMonth)
    rename _merge _mergeMonth

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 2. quarterly variables 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

preserve
    use "${RawMNEData}/CDProductivityQuarter", clear
    keep if FreqQuarter==1
    tempfile quarterly
    save `quarterly'
restore

generate YearQuarter = qofd(dofm(YearMonth))

merge m:1 IDlse YearQuarter using `quarterly', keepusing(Productivity* ProdGroup File FreqQuarter) update
    drop if _merge==2
    rename _merge _mergeQuarter

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 3. yearly variables 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

preserve
    use "${RawMNEData}/CDProductivityYear", clear
    keep if FreqYear==1
    tempfile yearly
    save `yearly'
restore

merge m:1 IDlse Year using `yearly', keepusing(Productivity* ProdGroup File FreqYear) update
    rename _merge _mergeYear

keep if (_mergeMonth==3) | (_mergeQuarter>=3 & _mergeQuarter<.) | (_mergeYear>=3 & _mergeYear<.) 

keep if Productivity!=. 

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 4. product group: ChannelFE
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

encode  ProdGroup, gen(ChannelFE)
replace ChannelFE = 0 if ChannelFE==. 
label define ChannelFE 0 "Not specified", modify

keep  IDlse YearMonth Productivity ProductivityStd ChannelFE 
order IDlse YearMonth Productivity ProductivityStd ChannelFE 

save "${TempData}/0105SalesProdOutcomes.dta", replace 
