/* 
This do file compares different skills between high-flyer managers and low-flyer managers.
The high-flyer measure used here is CA30.

Input:
    "${TempData}/0102_03HFMeasure.dta"      <== created in 0102_03 do file
    "${TempData}/0704_01SkillsAfterLDA.dta" <== created in 0704_01 py file 

Results:
    "${Results}/004ResultsBasedOnCA30/CA30_SkillsAfterLDA_HvsL_SUR.pdf"

RA: WWZ 
Time: 2025-04-16
*/

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 1. merge managers' quality measure and topic distribution after LDA
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

*-? managers' quality measure 
use "${TempData}/0102_03HFMeasure.dta", clear 
keep IDlseMHR CA30
rename IDlseMHR IDlse
duplicates drop 

*-? managers' topic distribution after LDA
merge 1:1 IDlse using "${TempData}/0704_01SkillsAfterLDA.dta", keep(match) nogenerate

*-? assign names to each topic 
label variable Topic1_3 "Project management skills"
label variable Topic2_3 "Strategy management skills"
label variable Topic3_3 "Talent management skills"

rename Topic1_3 Skills_Project 
rename Topic2_3 Skills_Strategy 
rename Topic3_3 Skills_Talent 

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 2. seemingly unrelated regressions
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-2-1. run regressions
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

capture noisily {
    sureg (Skills_Project = CA30) (Skills_Strategy = CA30) (Skills_Talent = CA30)
}
    //&? multicollinearity issue 

sureg (Skills_Strategy = CA30) (Skills_Talent = CA30)

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-2-2. store the results
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

*!! topic index 
generate topic = _n if inrange(_n, 1, 3)

*!! coefficients
generate coeff_SUR = _b["Skills_Strategy:CA30"] if _n ==2 
replace  coeff_SUR = _b["Skills_Talent:CA30"]   if _n ==3

*!! lower bound 
generate lb_SUR = _b["Skills_Strategy:CA30"] - 1.96 * _se["Skills_Strategy:CA30"] if _n ==2 
replace  lb_SUR = _b["Skills_Talent:CA30"]   - 1.96 * _se["Skills_Talent:CA30"]   if _n ==3

*!! upper bound 
generate ub_SUR = _b["Skills_Strategy:CA30"] + 1.96 * _se["Skills_Strategy:CA30"] if _n ==2 
replace  ub_SUR = _b["Skills_Talent:CA30"]   + 1.96 * _se["Skills_Talent:CA30"]   if _n ==3

graph twoway ///
    (bar coeff_SUR topic if inrange(topic, 2, 3), bfcolor(ebblue) barw(0.4)) ///
    (rcap ub_SUR lb_SUR topic if inrange(topic, 2, 3), lcolor(red) lwidth(medthick)) ///
    , legend(off) /// 
    xlabel(2 "Strategy skills" 3 "Talent skills", labsize(large)) ///
    xscale(range(1.5 3.5)) ylabel(0(0.02)0.13, grid gstyle(dot)) ///
    title("Mean difference between H and L-type managers", span pos(12)) xtitle("")

graph export "${Results}/004ResultsBasedOnCA30/CA30_SkillsAfterLDA_HvsL_SUR.pdf", replace as(pdf)
