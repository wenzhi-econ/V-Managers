/* 
This do file compares H- and L-type managers in active learning activities.

Input:
    "${TempData}/FinalAnalysisSample.dta" <== created in 0103_03 do file 
    "${RawMNEData}/ActiveLearn.dta"       <== raw data 

Results:
    "${Results}/FTActiveLearn.tex"

RA: WWZ 
Time: 2025-04-16
*/

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 1. obtain variables about workers' active learning in the firm
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

use "${TempData}/FinalAnalysisSample.dta", clear 
    //impt: use the analysis sample, i.e., keep only those workers who are in the event studies

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-1-1. obtain variables about active learning 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

merge 1:1 IDlse YearMonth using "${RawMNEData}/ActiveLearn.dta"
    keep if _merge==3
    drop _merge  

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-1-2. keep only relevant variables  
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

sort IDlse YearMonth
keep Year YearMonth IDlse Event_Time Rel_Time Post_Event CA30_LtoL CA30_LtoH CA30_HtoH CA30_HtoL IDMngr_Post NumRecommend NumCompleted NumSkills

replace  NumRecommend  = 0 if NumRecommend==.
replace  NumCompleted  = 0 if NumCompleted==.
replace  NumSkills     = 0 if NumSkills   ==.

keep if CA30_LtoL==1 | CA30_LtoH==1
    //impt: keep only LtoL and LtoH event workers

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-1-3. who should we focus on 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

sort IDlse YearMonth
bysort IDlse: egen temp = min(Post_Event)
generate q_PrePeriods = 1 - temp 
drop temp 

label variable q_PrePeriods "=1 if the employee has pre-event periods on the active learning variables"

keep if q_PrePeriods==1
    //impt: keep event workers with both pre- and post-event active learning variables 

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-1-4. collapse into employee-year level  
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

collapse ///
    (mean) Event_Time CA30_LtoL CA30_LtoH CA30_HtoH CA30_HtoL IDMngr_Post ///
    (max) NumRecommend NumCompleted NumSkills ///
    (max) Post_Event Max_Rel_Time=Rel_Time ///
    (min) Min_Rel_Time = Rel_Time, by(IDlse Year)

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-1-5. event group * post dummies   
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

generate CA30_LtoL_X_Post = CA30_LtoL * Post_Event
generate CA30_LtoH_X_Post = CA30_LtoH * Post_Event

label variable CA30_LtoL_X_Post "LtoL $\times$ post"
label variable CA30_LtoH_X_Post "LtoH $\times$ post"

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 2. outcome 1: raw yearly cumulative counts 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

reghdfe NumRecommend  Post_Event CA30_LtoH_X_Post, absorb(IDlse Year) cluster(IDMngr_Post) // 11.0346
reghdfe NumCompleted  Post_Event CA30_LtoH_X_Post, absorb(IDlse Year) cluster(IDMngr_Post) // -.6009834
reghdfe NumSkills     Post_Event CA30_LtoH_X_Post, absorb(IDlse Year) cluster(IDMngr_Post) // .4174785

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 3. outcome 2: yearly binary outcomes  
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

summarize NumRecommend if Post_Event==0, detail 
summarize NumCompleted if Post_Event==0, detail 
summarize NumSkills    if Post_Event==0, detail 

capture drop NumRecommendB NumCompletedB NumSkillsB
generate NumRecommendB = (NumRecommend>0)
generate NumCompletedB = (NumCompleted>=5)
generate NumSkillsB    = (NumSkills>=3)

reghdfe NumRecommendB  Post_Event CA30_LtoH_X_Post, absorb(IDlse Year) cluster(IDMngr_Post) // -.0064329
reghdfe NumCompletedB  Post_Event CA30_LtoH_X_Post, absorb(IDlse Year) cluster(IDMngr_Post) // -.0729841**
reghdfe NumSkillsB     Post_Event CA30_LtoH_X_Post, absorb(IDlse Year) cluster(IDMngr_Post) // -.0357118**
