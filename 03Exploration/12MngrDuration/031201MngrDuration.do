/* 
This do file investigates the distribution of post-event managers' duration at his work after the event, separately for different event groups.

RA: WWZ 
Time: 2025-01-06
*/

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 1. a cross-section of post-event managers 
*??         and their earliest involved event dates 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

use "${TempData}/04MainOutcomesInEventStudies.dta", clear 

keep if FT_Rel_Time!=. & FT_Mngr_both_WL2==1
    //&? a panel of event workers 

keep if FT_Rel_Time==0
    //&? a cross-sectional of event workers (at the time of event)

keep IDlseMHR YearMonth
duplicates drop 
    //&? all relevant (post-event manager, event time) pairs

sort IDlseMHR YearMonth
bysort IDlseMHR: egen min_EventTime = min(YearMonth)
format min_EventTime %tm
keep if YearMonth == min_EventTime
    //&? a cross-sectional of post-event managers

keep IDlseMHR min_EventTime
    //&? a list of post-event managers, and their earliest involved event dates 
    //&? 10,423 different managers

save "${TempData}/temp_PostEventMngrs.dta", replace

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 2. collect post-event managers' duration at work 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

use "${TempData}/04MainOutcomesInEventStudies.dta", clear 
drop IDlseMHR
rename IDlse IDlseMHR
merge m:1 IDlseMHR using "${TempData}/temp_PostEventMngrs.dta", keep(match) nogenerate
    //&? a full panel of relevant post-event managers 

keep if YearMonth >= min_EventTime
    //&? count the duration from the event time onwards

keep IDlseMHR YearMonth min_EventTime Func SubFunc Office Org4

sort IDlseMHR YearMonth
egen IDMHR_Work = group(IDlseMHR Func SubFunc Office Org4)

sort IDlseMHR YearMonth
bysort IDlseMHR: generate Change_Work = (IDMHR_Work[_n] != IDMHR_Work[_n-1])
bysort IDlseMHR: replace  Change_Work = 0 if _n==1
bysort IDlseMHR: generate IDWork      = sum(Change_Work) 

order IDlseMHR YearMonth IDMHR_Work Change_Work IDWork

generate FirstWork = (IDWork==0)
sort IDlseMHR YearMonth
bysort IDlseMHR: egen WorkDuration = total(FirstWork)

keep IDlseMHR WorkDuration
duplicates drop
    //&? 10,423 post-event managers' duration at work

save "${TempData}/temp_PostEventMngrsWorkDuration.dta", replace

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 3. find out managers' event groups  
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

use "${TempData}/04MainOutcomesInEventStudies.dta", clear 

keep if FT_Rel_Time!=. & FT_Mngr_both_WL2==1
    //&? a panel of event workers 

keep if FT_Rel_Time==0
    //&? a cross-sectional of event workers (at the time of event)

keep IDlseMHR FT_LtoL FT_LtoH FT_HtoH FT_HtoL

merge m:1 IDlseMHR using "${TempData}/temp_PostEventMngrsWorkDuration.dta", keep(match) nogenerate

save "${TempData}/temp_PostEventMngrsWorkDurationEventGroups.dta", replace

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 4. plot the distribution  
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-4-1. by event groups, weighted version 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

use "${TempData}/temp_PostEventMngrsWorkDurationEventGroups.dta", clear

graph twoway ///
    (kdensity WorkDuration if FT_LtoL==1) ///
    (kdensity WorkDuration if FT_LtoH==1) ///
    (kdensity WorkDuration if FT_HtoH==1) ///
    (kdensity WorkDuration if FT_HtoL==1), ///
    scheme(tab2) ///
    legend(label(1 "LtoL") label(2 "LtoH") label(3 "HtoH") label(4 "HtoL")) ///
    xtitle("Duration at work after event") ///
    ytitle("Frequency") ///
    title("Managers' duration at work after event, by event groups") 

graph export "${Results}/PostEventMngrDuration.png", replace as(png)

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-4-2. by high-flyer status, cross-sectional version 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

use "${TempData}/temp_PostEventMngrsWorkDurationEventGroups.dta", clear

generate EarlyAgeM = 0
replace  EarlyAgeM = 1 if FT_HtoH==1 | FT_LtoH==1

keep IDlseMHR EarlyAgeM WorkDuration
duplicates drop 

graph twoway ///
    (kdensity WorkDuration if EarlyAgeM==1, lcolor("237 68 74")) ///
    (kdensity WorkDuration if EarlyAgeM==0, lcolor("145 179 215")) ///
    , ///
    scheme(tab2) ///
    legend(label(1 "High-flyers") label(2 "Low-flyers")) ///
    xtitle("Duration at work after event") ///
    ytitle("Frequency") ///
    title("Managers' duration at work after event, by manager types") 

graph export "${Results}/PostEventMngrDuration_CrossSection.png", replace as(png)


ksmirnov WorkDuration, by(EarlyAgeM)
/* 
Two-sample Kolmogorovâ€“Smirnov test for equality of distribution functions

Smaller group             D     p-value  
---------------------------------------
0                    0.0000       1.000
1                   -0.1366       0.000
Combined K-S         0.1366       0.000

Note: Ties exist in combined dataset;
      there are 122 unique values out of 10423 observations.
*/

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 5. average duration   
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

use "${TempData}/temp_PostEventMngrsWorkDurationEventGroups.dta", clear

summarize WorkDuration if FT_LtoL==1, detail // mean 26.03784
summarize WorkDuration if FT_LtoH==1, detail // mean 18.19769
summarize WorkDuration if FT_HtoH==1, detail // mean 17.55562
summarize WorkDuration if FT_HtoL==1, detail // mean 21.51225

