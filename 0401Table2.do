/* 
The codes are copied from "2.3.SummaryStats.do".
I made a lot of changes to the original codes to keep only the relevant ones. 

Input files:
    "${FinalData}/AllSnapshotMCulture.dta" (main)
    "${FinalData}/EducationMax.dta" (raw data, education information)
    "${TempData}/ProductivityManagers.dta"

Output files:
    "${Results}/Table2_SummaryStatistics.tex"

RA: WWZ 
Time: 26/08/2024
*/

capture log close 

log using "${Results}/logfile_20240826_Table2", replace text

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 0. Prepare the dataset
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

use  "${FinalData}/AllSnapshotMCulture.dta", clear
merge 1:1 IDlse YearMonth using  "${TempData}/ProductivityManagers.dta", keepusing(ProductivityStd Productivity)
drop if _merge ==2 
drop _merge 

merge m:1 IDlse using "${FinalData}/EducationMax.dta", keepusing(QualHigh FieldHigh1 FieldHigh2 FieldHigh3)
drop if _merge ==2 
drop _merge

xtset IDlse YearMonth 

egen tag_Mngr = tag(IDlseMHR)
egen tag_Ind = tag(IDlse)

keep ///
    IDlse IDlseMHR YearMonth tag_Mngr tag_Ind ///
    Female AgeBand FieldHigh1 FieldHigh2 FieldHigh3 QualHigh ///
    Tenure WL ChangeM TeamSize ///
    ChangeSalaryGradeC TransferSJLLC PromWLC LeaverPerm LogPayBonus Bonus Pay VPA Productivity ISOCode

order ///
    IDlse IDlseMHR YearMonth tag_Mngr tag_Ind ///
    Female AgeBand FieldHigh1 FieldHigh2 FieldHigh3 QualHigh ///
    Tenure WL ChangeM TeamSize ///
    ChangeSalaryGradeC TransferSJLLC PromWLC LeaverPerm LogPayBonus Bonus Pay VPA Productivity ISOCode

save "${FinalData}/temp_table2.dta", replace 

/* capture log close 

log using "${Results}/logfile_20240826_Table2", replace text */

use "${FinalData}/temp_table2.dta", clear 

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 1. Panel A of Table 2: Demographics
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s1_1. Female
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
label variable Female "Female"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s1_2. Cohort
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
replace AgeBand =  1 if  AgeBand==7 //18
replace AgeBand =  4 if  AgeBand>4 & AgeBand!=. // above 50
tab AgeBand, gen(Cohort)
label variable Cohort1 "Share in Cohort 18-29"  
label variable Cohort2 "Share in Cohort 30-39"
label variable Cohort3 "Share in Cohort 40-49"
label variable Cohort4 "Share in Cohort 50+"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s1_3. Education
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

generate Econ = (FieldHigh1 == 4 | FieldHigh2 == 4 | FieldHigh3 == 4) if FieldHigh1!=.
label variable Econ "Econ, Business, and Admin"

generate Sci = (FieldHigh1 == 5 | FieldHigh1 == 7 | FieldHigh1 == 9 | FieldHigh1 == 14 | FieldHigh1 == 15 | FieldHigh1 == 17 | ///
FieldHigh2 == 5 | FieldHigh2 == 7 | FieldHigh2 == 9 | FieldHigh2 == 14 | FieldHigh2 == 15 | FieldHigh2 == 17 | ///
FieldHigh3 == 5 | FieldHigh3 == 7 | FieldHigh3 == 9 | FieldHigh3 == 14 | FieldHigh3 == 15 | FieldHigh3 == 17) if FieldHigh1!=.
label variable Sci "Sci, Engin, Math, and Stat"

generate Hum = (FieldHigh1 == 6 | FieldHigh2 == 6 | FieldHigh3 == 6 | FieldHigh1 == 11 | FieldHigh2 == 11 | FieldHigh3 == 11 | FieldHigh1 == 12 | FieldHigh2 == 12 | FieldHigh3 == 12 | FieldHigh1 == 13 | FieldHigh2 == 13 | FieldHigh3 == 13 | FieldHigh1 == 19 | FieldHigh2 == 19 | FieldHigh3 == 19) if FieldHigh1!=.
label variable Hum "Social Sciences and Humanities"

generate Other = (Econ ==0 & Sci ==0 & Hum ==0  )  if FieldHigh1!=.
label variable Other "Other Educ"

generate Missing = FieldHigh1 ==. 
label variable Missing "Missing Education"

generate Bachelor =       QualHigh >= 10 if QualHigh!=.
generate MBA =            QualHigh == 13 if QualHigh!=.
generate AboveSecondary = QualHigh >= 6  if QualHigh!=.

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s1_4. Produce the table
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
eststo clear 

estpost summarize Female Cohort1 Cohort2 Cohort3 Cohort4 Econ Sci Hum Other, detail
esttab using "${Results}/Table2_SummaryStatistics.tex", ///
    replace ci(3) label nonotes noobs nomtitles collabels(,none) nonumbers ///
    cells("mean(fmt(%9.2fc)) sd(fmt(%8.1fc)) p1(fmt(%8.1fc)) p99(fmt(%8.1fc)) count(fmt(%12.0fc) )") ///
    prehead("\begin{tabular}{lccccc}" "\toprule" "\toprule" "& \multicolumn{1}{c}{Mean} & \multicolumn{1}{c}{SD} & \multicolumn{1}{c}{P1} & \multicolumn{1}{c}{P99} & \multicolumn{1}{c}{N} \\ ") ///
    posthead("\multicolumn{6}{c}{\emph{Panel (a): gender, age and education}} \\") ///
    prefoot(" ") postfoot("\hline")

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 2. Panel B of Table 2: Work-related variables
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s2_1. Tenure
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
label variable Tenure "Tenure (years)"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s2_2. WL
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
replace WL = 3 if WL > 3
tab WL, gen(WL)
label variable WL1 "Share in Work-level 1" 
label variable WL2 "Share in Work-level 2" 
label variable WL3 "Share in Work-level 3+" 

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s2_3. # of obs
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
generate o = 1
bys IDlse: egen NoMonths   = sum(o)
bys IDlse: egen ChangeMTot = sum(ChangeM)

replace TeamSize = .   if tag_Mngr==0 
replace NoMonths = .   if tag_Ind==0 
replace ChangeMTot = . if tag_Ind==0 

label variable NoMonths   "No. of months per worker"
label variable TeamSize   "No. of workers per supervisor"
label variable ChangeMTot "No. of supervisors per worker"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s2_4. Produce the table
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
estpost summarize Tenure WL1 WL2 WL3 NoMonths ChangeMTot TeamSize, detail
esttab using "${Results}/Table2_SummaryStatistics.tex", ///
    append ci(3) label nonotes noobs nomtitles collabels(,none) nonumbers ///
    cells("mean(fmt(%9.2fc)) sd(fmt(%8.1fc)) p1(fmt(%8.1fc)) p99(fmt(%8.1fc)) count(fmt(%12.0fc) )") ///
    prehead(" ") /// 
    posthead("\multicolumn{6}{c}{\emph{Panel (b): tenure, hierarchy and team size}} \\") ///
    postfoot("\hline") prefoot(" ")


*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 3. Panel C of Table 2: Outcome variables
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s3_1. ChangeSalaryGradeC TransferSJLLC PromWLC
*-? Original variables are at individual level, which are transformed to 
*-?   make sure each individual has only one non-missing value 
*-?   to avoid double summary
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
foreach v in ChangeSalaryGradeC TransferSJLLC PromWLC {
	bysort IDlse: egen `v'm = max(`v')
	replace `v'm = . if tag_Ind==0 
}
label variable ChangeSalaryGradeCm "Number of salary grade increases"
label variable TransferSJLLCm      "Number of lateral job transfers"
label variable PromWLCm            "Number of promotions (work-level)"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s3_2. LeaverPerm
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
label variable LeaverPerm "Monthly Exit"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s3_3. LogPayBonus BonusPay
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
label variable LogPayBonus "Pay + bonus (logs)"

generate BonusPay = Bonus/Pay
label variable BonusPay    "Bonus over Pay"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s3_4. VPA
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
label variable VPA "Perf. appraisals"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s3_5. LogP
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
generate LogP = log(Productivity + 1) if ISOCode=="IND"
label variable LogP "Productivity (sales in logs)"


*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s3_6. Produce the table
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
estpost summarize ChangeSalaryGradeCm TransferSJLLCm PromWLCm LeaverPerm LogPayBonus BonusPay VPA LogP , detail
    
esttab using "${Results}/Table2_SummaryStatistics.tex", ///
    append ci(3) label nonotes noobs nomtitles collabels(,none) nonumbers ///
    cells("mean(fmt(%9.2fc)) sd(fmt(%8.1fc)) p1(fmt(%8.1fc)) p99(fmt(%8.1fc)) count(fmt(%12.0fc) )") ///
    prehead(" ") ///
    posthead("\multicolumn{6}{c}{\emph{Panel (c): outcome variables}} \\") ///
    prefoot(" ") ///
    postfoot("\hline" "\end{tabular}" "\begin{tablenotes}" "\footnotesize" "\item" "An observation is a worker-month-year. The data contain personnel records for the entire white-collar employee base from January 2011 until December 2021. In Panel (a) cohort refers to the age group and education data is only available for a subset of workers. In Panel (b) work level denotes the hierarchical tier (from level 1 at the bottom to level 6). In Panel (c) salary information is only available since January 2015 and the data on performance ratings start in January 2017." "\end{tablenotes}") 

log close