/* 

The codes are copied from "1.1.Descriptives Tables.do".
I made a lot of changes to the original codes to keep only the relevant ones. 

Input files:
    "$${TempData}/MType.dta" (main)
    "${FinalData}/EducationMax.dta" (raw data, education information)
    "$${TempData}/m2.dta" (no idea what it does, but it provides a key varible HF)

Output files:
    "${Results}/Table3_MngrHFvsNHFStatistics.tex"

RA: WWZ 
Time: 26/08/2024
*/

capture log close 

log using "${Results}/logfile_20240826_Table3", replace text

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 1: obtain the final dataset for this table 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

use "${TempData}/MType.dta", clear 

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s1_1: education-related variables
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
merge m:1 IDlseMHR  using "${FinalData}/EducationMax.dta" , keepusing(QualHigh FieldHigh1 FieldHigh2 FieldHigh3)
drop if _merge ==2 
drop _merge 

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s1_2: HF variable
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
merge m:1 IDlseMHR using "${TempData}/m2.dta", keepusing(mT HF) // created in 2.4.MTypeRotation.do 
drop _merge 

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s1_3: individual tag to use in demographic summary statistics
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
bys IDlseMHR: egen minYM = min(YearMonth)
generate oo = YearMonth == minYM
order oo 

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s1_4: keep only those managers used in our event study design 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
merge m:1 IDlseMHR using "${TempData}/mSample.dta", keepusing(minAge)
keep if _merge==3 

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 2: generate relevant variables 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s2_1: Monthly salary growth 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
xtset IDlseMHR YearMonth 
gen PayGrowth = d.LogPayBonusM 
label variable PayGrowth "Monthly salary growth"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s2_2: Promotion work-level 3
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
gen WLAgg = WLM
replace WLAgg = 3 if WLM>3 & WLM!=.
tab WLAgg , gen(WLAgg)
label variable WLAgg3 "Promotion work-level 3" 

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s2_3: Perf. rating (1-150)
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
label variable VPAM "Perf. rating (1-150)"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s2_4: Effective leader (survey)
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
label variable LineManagerMean "Effective leader (survey)"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s2_5: Female 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
label variable FemaleM "Female"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s2_6: MBA 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
gen Bachelor       =    QualHigh >=10 if QualHigh!=.
gen MBA            =    QualHigh ==13 if QualHigh!=.
gen AboveSecondary = QualHigh >=6 if QualHigh!=.

label variable MBA "MBA"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s2_7: field of study 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
gen Econ = (FieldHigh1 == 4 | FieldHigh2 == 4 | FieldHigh3 == 4) if FieldHigh1!=.
label variable Econ "Econ, Business, and Admin"

gen Sci = (FieldHigh1 == 5 | FieldHigh1 == 7 | FieldHigh1 == 9 | FieldHigh1 == 14 | FieldHigh1 == 15 | FieldHigh1 == 17 | ///
    FieldHigh2 == 5 | FieldHigh2 == 7 | FieldHigh2 == 9 | FieldHigh2 == 14 | FieldHigh2 == 15 | FieldHigh2 == 17 | ///
    FieldHigh3 == 5 | FieldHigh3 == 7 | FieldHigh3 == 9 | FieldHigh3 == 14 | FieldHigh3 == 15 | FieldHigh3 == 17) if FieldHigh1!=.
label variable Sci "Sci, Tech, Engin, and Math"

gen Hum = (FieldHigh1 == 6 | FieldHigh2 == 6 | FieldHigh3 == 6 | FieldHigh1 == 11 | FieldHigh2 == 11 | FieldHigh3 == 11 | ///
    FieldHigh1 == 12 | FieldHigh2 == 12 | FieldHigh3 == 12 | FieldHigh1 == 13 | FieldHigh2 == 13 | FieldHigh3 == 13 | ///
    FieldHigh1 == 19 | FieldHigh2 == 19 | FieldHigh3 == 19) if FieldHigh1!=.
label variable Hum "Social Sciences and Humanities"

gen Other = (Econ ==0 & Sci ==0 & Hum ==0  )  if FieldHigh1!=.
label variable Other "Other Educ"

gen Missing = FieldHigh1 ==. 
label variable Missing "Missing Education"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s2_8: Mid-career recruit 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
bys IDlseMHR : egen FF= min(YearMonth)
bys IDlseMHR : egen FirstWL = mean(cond(YearMonth==FF, WLM, .)) // first WL observed 
bys IDlseMHR : egen FirstTenure = mean(cond(YearMonth==FF, TenureM, .)) // tenure in first month observed 

gen MidCareerHire = FirstWL>1 & FirstTenure<=1 & WLM!=. 

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 3: produce the summary table 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s3_1: performance-related variables  
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

*!! important: we only consider the performance when managers achieves their maximum work-level 
*!! threfore, we need to restrict the sample in panel a of this table 

bys IDlseMHR: egen YearMaxWLM = min(cond(WLM == MaxWLM, YearMonth, .))
format YearMaxWLM %tm
gen Post = (YearMonth >= YearMaxWLM) if YearMonth != .

preserve 
    *!! s-3-1: keep only performance after managers achieves their maximum work-level
    keep if Post == 1 // sample restriction only to performance-releated variables 

    *!! s-3-2: average over all post periods 
    collapse EarlyAgeM PayGrowth WLAgg3 VPAM LineManagerMean, by(IDlseMHR)

    label variable PayGrowth       "Monthly salary growth"
    label variable WLAgg3          "Promotion work-level 3" 
    label variable VPAM            "Perf. rating (1-150)"
    label variable LineManagerMean "Effective leader (survey)"

    balancetable EarlyAgeM PayGrowth WLAgg3 VPAM LineManagerMean ///
        using "${Results}/Table3_MngrHFvsNHFStatistics.tex", ///
        pval varla vce(cluster IDlseMHR) ctitles("Not High Flyer" "High Flyer" "Difference") ///
        prehead("\begin{tabular}{l*{3}{c}} \hline\hline") ///
        posthead("\hline \\ \multicolumn{4}{c}{\textit{Panel (a): performance after high-flyer status is determined}} \\\\[-1ex]") ///
        postfoot(" ") /// 
        noli noobs replace

restore 

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s3_2: demographics variables  
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

balancetable HF FemaleM MBA Econ Sci Hum Other MidCareerHire if oo==1 ///
    using "${Results}/Table3_MngrHFvsNHFStatistics.tex", ///
    pval varla vce(cluster IDlseMHR)  ///
    noli nonum append ///
    prehead(" ") ///
    posthead("\hline \\ \multicolumn{4}{c}{\textit{Panel (b): demographics}} \\\\[-1ex]") ///
    postfoot("\hline" "\end{tabular}" "\begin{tablenotes}" "\footnotesize" "\item" ///
    "Notes. Showing mean and standard deviations (in parentheses) and p-values for the difference in means. The  difference in means is computed using standard errors clustered by manager. \emph{Perf. rating} refers to the performance assessment given annually to each employee; \emph{Effective leader (survey)} refers to the workers' anonymous upward feedback on the managers' leadership; and \emph{Mid-career recruit} refers to managers who have been hired directly as managers by the firm (at work-level 2 instead of work-level 1)." "\end{tablenotes}")


log close