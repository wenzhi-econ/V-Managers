/* 
This do file constructs variables that describe relationships between managers and workers, such as if they share the same office, if their share the same gender.
It also constructs relevant variables for office characteristics and manager characteristics.

Input:
    "${RawMNEData}/AllSnapshotWC.dta"

Output: 
    "${TempData}/05MngrWorkerRelations.dta"
    "${TempData}/temp_Mngr_Characteristics.dta"

RA: WWZ 
Time: 2024-10-07
*/

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 1. get managers' characteristics 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

use "${RawMNEData}/AllSnapshotWC.dta", clear 
sort IDlse YearMonth

global Mngr_Vars OfficeCode Female Tenure 

keep IDlse YearMonth $Mngr_Vars

foreach var in $Mngr_Vars {
    rename `var' `var'M 
}
rename IDlse IDlseMHR

label drop _all 

save "${TempData}/temp_Mngr_Characteristics.dta", replace 

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 2. merge managers' characteristics back into the main dataset 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

use "${RawMNEData}/AllSnapshotWC.dta", clear 
xtset IDlse YearMonth
sort  IDlse YearMonth

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-2-1. impute missing manager id 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

foreach var in IDlseMHR {
	replace `var' = l1.`var' if IDlseMHR==. & l1.IDlseMHR!=. 
	replace `var' = f1.`var' if IDlseMHR==. & f1.IDlseMHR!=. & l1.IDlseMHR==. 
}

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-2-2. merge 
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

merge m:1 IDlseMHR YearMonth using "${TempData}/temp_Mngr_Characteristics.dta", keep(match master) nogenerate

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 3. get the relationship variables we need  
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-3-1. SameOffice
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

generate DiffOffice = 0
replace  DiffOffice  = 1 if OfficeCode!=OfficeCodeM
replace  DiffOffice  = . if ((OfficeCode==.)  | (OfficeCodeM==.))
label variable DiffOffice "=1 if manager in different office"

generate SameOffice = 1 - DiffOffice 
label variable SameOffice "=1 if manager in same office"

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-3-2. SameGender
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

generate SameGender = 0
replace  SameGender = 1 if Female==FemaleM
replace  SameGender = . if ((Female==.) | (FemaleM==.))
label variable SameGender "=1 if employee has same gender as manager"

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step 3. get other relevant variables 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?
*-? s-4-1. OfficeSize
*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?*-?

sort Office YearMonth
bysort Office YearMonth: egen OfficeSize = count(IDlse)

*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??
*?? step z. save the dataset 
*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??*??

keep  IDlse YearMonth IDlseMHR TenureM SameOffice SameGender OfficeSize
order IDlse YearMonth IDlseMHR TenureM SameOffice SameGender OfficeSize
sort  IDlse YearMonth

compress 
save "${TempData}/05MngrWorkerRelations.dta", replace 


